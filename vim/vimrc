call plug#begin('~/.vim/plugins')
Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
Plug 'junegunn/fzf.vim'
Plug 'lambdalisue/gina.vim'
Plug 'mhinz/vim-signify'
Plug 'tpope/vim-commentary'
Plug 'machakann/vim-sandwich'
Plug 'junegunn/vim-easy-align'
Plug 'aonemd/vim-lsc'
Plug 'aonemd/fmt.vim'
Plug 'sheerun/vim-polyglot'
Plug 'aonemd/vim-printer'
Plug 'aonemd/decalogue.vim'
Plug 'mattn/emmet-vim'
Plug 'ap/vim-css-color'
Plug 'junegunn/goyo.vim'
Plug 'aonemd/dazzle.vim'
Plug 'aonemd/vim-lambdify'
Plug 'aonemd/kuroi.vim'
Plug 'aonemd/quietlight.vim'
Plug 'pgdouyon/vim-yin-yang'
Plug 'aonemd/atlas.vim'
call plug#end()

set lazyredraw                                     "lazily redraw screen while executing macros, and other commands
set ttyfast                                        "more characters will be sent to the screen for redrawing
set ttimeout                                       "time waited for key press(es) to complete...
set ttimeoutlen=50                                 " ...makes for a faster key response
set noswapfile                                     "disable swap files
set viminfo+=n/tmp/.viminfo                        "path to store .viminfo files
set autoread                                       "automatically read changes in the file
set hidden                                         "hide buffers instead of closing them even if they contain unwritten changes
set backspace=indent,eol,start                     "make backspace behave properly in insert mode
set clipboard=unnamedplus                          "requires has('unnamedplus') to be 1
set wildmenu                                       "better menu with completion in command mode
set wildmode=longest:full,full
set shortmess+=c                                   "don't give completion messages like 'match 1 of 2' or 'The only match'
set nowrap                                         "disable soft wrap for lines
set scrolloff=2                                    "always show 2 lines above/below the cursor
set showcmd                                        "display incomplete commands
set laststatus=2                                   "always display the status bar
set number relativenumber                          "display line numbers
set signcolumn=yes                                 "show signs
set cursorline                                     "highlight current line
set colorcolumn=81                                 "display text width column
set splitbelow                                     "vertical splits will be at the bottom
set splitright                                     "horizontal splits will be to the right
set termwinsize=15x                                "terminal will be 15 units long

set autoindent                                     "always set autoindenting on
set formatoptions-=cro                             "disable auto comments on new lines
set formatoptions+=j                               "remove a comment leader when joining lines
set tabstop=2 shiftwidth=2 softtabstop=2 expandtab "use two spaces for indentation

set incsearch                                      "incremental search highlight
set ignorecase                                     "searches are case insensitive...
set smartcase                                      " ..unless they contain at least one capital letter
set hlsearch                                       "highlight search patterns

"read project `.vimrc` file
set exrc
set secure

set foldmethod=indent
set nofoldenable

augroup SyntaxAutocmds
  autocmd! FileType c          setlocal ts=4 sts=4 sw=4 noexpandtab
  autocmd! FileType java       setlocal ts=4 sts=4 sw=4 expandtab
  autocmd! FileType make       setlocal ts=8 sts=8 sw=8 noexpandtab
  autocmd! FileType ruby       setlocal makeprg=ruby\ %
  autocmd! FileType go         setlocal makeprg=go\ run\ %
  autocmd! FileType javascript setlocal makeprg=node\ %
  autocmd! FileType typescript setlocal makeprg=ts-node\ %
  autocmd! FileType scheme     setlocal makeprg=chicken-csi\ -s\ %
  autocmd! FileType *.h        setlocal filetype=c
  autocmd! FileType css        setlocal iskeyword+=-
  autocmd! FileType scss       setlocal iskeyword+=-
  autocmd! FileType vue        syntax sync fromstart
augroup END

set background=dark
set termguicolors                                  "or enable 256 colors: set t_Co=256
colorscheme kuroi

"status line: modifiedflag, charcount, filepercent, filepath
set statusline=%=%m\ %c\ %P\ %f

"remove current line highlight in unfocused window
autocmd! VimEnter,WinEnter,BufWinEnter,FocusGained,CmdwinEnter * set cul
autocmd! WinLeave,FocusLost,CmdwinLeave * set nocul

"resize Vim on Tmux split
autocmd! VimResized * :wincmd =

"The Leader
let mapleader="\<Space>"

nnoremap ! :!
nnoremap <leader>m :make<cr>

"trailing whitespace
function! StripTrailingWhitespace()
  if !&binary && &filetype != 'diff'
    let w:winview = winsaveview()
    %s/\(\s\+$\)\|\($\n\s*\)\+\%$//e
    if exists('w:winview') | call winrestview(w:winview) | endif
  endif
endfunction

function! SaveAuxiliary()
  :call StripTrailingWhitespace()
  :Fmt
  :w
endfunction
nnoremap <leader>w :call SaveAuxiliary()<cr>

"easily move between end of current line
map H ^
map L g_
"replace the word under cursor
nnoremap <leader>* :%s/\<<c-r><c-w>\>//g<left><left>
"toggle showing hidden characters
nnoremap <silent> <leader>s :set nolist!<cr>
"toggle spell checking
nnoremap <leader>ss :setlocal spell!<cr>
"toggle RTL mode
nnoremap <silent> <leader>l :set arabic!<cr>
"override system files by typing :w!!
cnoremap w!! %!sudo tee > /dev/null %
"remove search highlight
nmap <leader>q :nohlsearch<CR>
"execute and replace current line in $SHELL with the output
nnoremap Q !!$SHELL<cr>
"set cwd to the directory of the current file
nnoremap <leader>cd :lcd %:p:h<cr>

"move lines around
nnoremap <leader>k :m-2<cr>==
nnoremap <leader>j :m+<cr>==
xnoremap <leader>k :m-2<cr>gv=gv
xnoremap <leader>j :m'>+<cr>gv=gv

"custom abbreviations
"inoreabbrev ignores custom mappings when executing the abbreviations
inoreabbrev cf () {<CR>}<UP><LEFT>
inoreabbrev fn fn() -> () {<CR>}<UP><ESC>f(i
inoreabbrev af () => {<CR>}<UP>
inoreabbrev jf function() {<CR>}<UP><RIGHT><RIGHT><RIGHT><RIGHT><RIGHT><RIGHT><RIGHT>
inoreabbrev df def()<CR>end<UP>

"autoclose tags
inoremap ( ()<Left>
inoremap (; ();<Left><Left>
inoremap (, (),<Left><Left>
inoremap <expr> )  strpart(getline('.'), col('.')-1, 1) == ")" ? "\<Right>" : ")"
inoremap { {}<Left>
inoremap {<CR> {<CR>}<ESC>O
inoremap {;<CR> {<CR>};<ESC>O
inoremap [ []<Left>
inoremap [<CR> [<CR>]<ESC>O
inoremap [;<CR> [<CR>];<ESC>O
inoremap <expr> " strpart(getline('.'), col('.')-1, 1) == "\"" ? "\<Right>" : "\"\"\<Left>"

"keep text selected after indentation
vnoremap < <gv
vnoremap > >gv

nnoremap <leader>B :enew<cr>
nnoremap <Tab> :bnext<cr>
nnoremap <S-Tab> :bprevious<cr>
nnoremap <leader>bq :bp <bar> bd! #<cr>
nnoremap <leader>ba :bufdo bd!<cr>
"cycle between last two open buffers
nnoremap <leader><leader> <c-^>

"commandments
let g:decalogue_commandments = {
      \ 'Git push': 'Gina push',
      \ 'Git status': 'Gina status -s',
      \ 'Git patch': 'Gina patch --oneside --opener=edit %',
      \ 'Rust test': 'term cargo t',
      \ 'Rust check': 'term cargo c',
      \ 'Rust run': 'term cargo r',
      \ 'C run': '!gcc % -o a.out; ./a.out',
      \ }

"fzf
let g:fzf_layout       = { 'down': '~30%' }
let g:fzf_tags_command = 'ctags -Rf .git/tags'

nnoremap <leader>p :FZF<cr>
nnoremap <leader>o :Lines<cr>
nnoremap <leader>t :Tags<cr>
nnoremap <leader>r :Buffers<cr>

nnoremap \  :Rg<space>
nnoremap \| :Rg <C-R><C-W><cr>:cw<cr>

"Autocomplete
set completeopt=menu,menuone,popup,noinsert "better insert mode completions
set completepopup=border:off,highlight:InfoPopup
"confirm selection using Enter key
inoremap <expr> <CR> pumvisible() ? "\<C-Y>" : "\<CR>"
" trigger omnicompletion with ctrl-space
inoremap <Nul> <C-x><C-o><C-o>

"LSC
let g:lsc_enable_autocomplete  = v:true
let g:lsc_enable_diagnostics   = v:true
let g:lsc_reference_highlights = v:false
let g:lsc_trace_level          = 'off'
let g:lsc_auto_completeopt     = 'menu,menuone,popup,noinsert'

let g:lsc_auto_map = {
 \  'GoToDefinition': 'gd',
 \  'FindReferences': 'gr',
 \  'FindImplementations': 'gI',
 \  'Rename': 'gR',
 \  'ShowHover': v:true,
 \  'FindCodeActions': 'ga',
 \  'Completion': 'omnifunc',
 \}

nnoremap gs :LSClientAllDiagnostics<cr>

let g:lsc_server_commands = {
 \  'c': {
 \    'command': 'clangd --background-index',
 \    'suppress_stderr': v:true,
 \  },
 \  'cpp': {
 \    'command': 'clangd --background-index',
 \    'suppress_stderr': v:true,
 \  },
 \  'rust': {
 \    'command': 'rls',
 \    'suppress_stderr': v:true,
 \  },
 \  'go': {
 \    'command': 'gopls',
 \    'suppress_stderr': v:true,
 \  },
 \  'javascript': {
 \    'command': 'typescript-language-server --stdio',
 \    'log_level': -1,
 \    'suppress_stderr': v:true,
 \  },
 \  'typescript': {
 \    'command': 'typescript-language-server --stdio',
 \    'log_level': -1,
 \    'suppress_stderr': v:true,
 \  },
 \  'ruby': {
 \    'command': 'solargraph stdio',
 \    'log_level': -1,
 \    'suppress_stderr': v:true,
 \  },
 \  'vim': {
 \    'command': 'vim-language-server --stdio',
 \    'log_level': -1,
 \    'suppress_stderr': v:true,
 \  },
 \}

"Ctags
set tags+=.git/tags
nnoremap <leader>ct :!ctags -Rf .git/tags<cr><cr>

"Signify
let g:signify_vcs_list = ['git', 'hg']
nmap <c-n> <plug>(signify-next-hunk)
nmap <c-p> <plug>(signify-prev-hunk)

"EasyAlign
nmap <leader>a <Plug>(EasyAlign)
xmap <leader>a <Plug>(EasyAlign)

"vim-polyglot
let g:go_highlight_functions = 1

"Dazzle
autocmd BufNewFile,BufRead *.pres :Dazzle

"netrw
let g:netrw_banner=0
let g:netrw_winsize=20
let g:netrw_liststyle=3  "tree
let g:netrw_localrmdir='rm -r'
nnoremap <leader>n :Lexplore<CR>
nnoremap <silent><leader>- :Lexplore %:h<CR><CR>

"move to the split in the direction shown, or create a new split
nnoremap <silent> <C-h> :call WinMove('h')<cr>
nnoremap <silent> <C-j> :call WinMove('j')<cr>
nnoremap <silent> <C-k> :call WinMove('k')<cr>
nnoremap <silent> <C-l> :call WinMove('l')<cr>
function! WinMove(key)
  let t:curwin = winnr()
  exec "wincmd ".a:key
  if (t:curwin == winnr())
    if (match(a:key,'[jk]'))
      wincmd v
    else
      wincmd s
    endif
    exec "wincmd ".a:key
  endif
endfunction

"smooth scroll
nnoremap <silent> <c-u> :call SmoothScroll(1)<cr>
nnoremap <silent> <c-d> :call SmoothScroll(0)<cr>
function! SmoothScroll(up)
  execute "normal " . (a:up ? "\<c-y>" : "\<c-e>")
  redraw
  for l:count in range(3, &scroll, 2)
    sleep 7m
    execute "normal " . (a:up ? "\<c-y>" : "\<c-e>")
    redraw
  endfor
  " bring the cursor in the middle of screen
  execute "normal M"
endfunction

"Goyo
let g:goyo_width = 81
nnoremap <leader>g :Goyo<cr>
if !exists('*s:goyo_leave')
  function s:goyo_leave()
    source $MYVIMRC
  endfunction
endif
autocmd! User GoyoLeave nested call <SID>goyo_leave()

"highlight repeated lines. Clear with `:e`
function! HighlightRepeats() range
  let lineCounts = {}
  let lineNum = a:firstline
  while lineNum <= a:lastline
    let lineText = getline(lineNum)
    if lineText != ""
      let lineCounts[lineText] = (has_key(lineCounts, lineText) ? lineCounts[lineText] : 0) + 1
    endif
    let lineNum = lineNum + 1
  endwhile
  exe 'syn clear Repeat'
  for lineText in keys(lineCounts)
    if lineCounts[lineText] >= 2
      exe 'syn match Repeat "^' . escape(lineText, '".\^$*[]') . '$"'
    endif
  endfor
endfunction
command! -range=% HighlightRepeats <line1>,<line2>call HighlightRepeats()

"maximize current split
nnoremap <C-W>O :call MaximizeToggle()<CR>
function! MaximizeToggle()
  if exists("s:maximize_session")
    exec "source " . s:maximize_session
    call delete(s:maximize_session)
    unlet s:maximize_session
    let &hidden=s:maximize_hidden_save
    unlet s:maximize_hidden_save
  else
    let s:maximize_hidden_save = &hidden
    let s:maximize_session = tempname()
    set hidden
    exec "mksession! " . s:maximize_session
    only
  endif
endfunction

"scroll popup windows
nnoremap <silent> <PageDown> :call ScrollPopup(3)<CR>
nnoremap <silent> <PageUp> :call ScrollPopup(-3)<CR>
function! ScrollPopup(val)
  let winid = popup_list()
  if winid == [] | return 0 | endif

  let pos = popup_getpos(winid[0])
  "if there's no scrollbar visible, exit.
  if pos.scrollbar == 0 | return 0 | endif

  "set the first and last lines of the popup buffer.
  "https://fortime.ws/blog/2020/03/14/20200312-01/
  let new_firstline = pos.firstline + a:val
  let new_lastline = str2nr(trim(win_execute(winid[0], "echo line ('$')")))
  if new_firstline < 1
    let new_firstline = 1
  elseif pos.lastline + a:val > new_lastline
    let new_firstline = new_firstline - a:val + new_lastline - pos.lastline
  endif

  call popup_setoptions(winid[0], #{
        \ firstline: new_firstline,
        \ minwidth: pos.core_width,
        \ maxwidth: pos.core_width + 1,
        \ }) "constrain min and maxwidth so that they don't change when scrolling.
endfunction

function! SynStack()
  if !exists("*synstack")
    return
  endif
  echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
endfunc
